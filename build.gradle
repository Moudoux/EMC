buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'SpongePowered'
            url = 'http://repo.spongepowered.org/maven'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.0"
    }
}

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'org.spongepowered.mixin'
apply plugin: "com.github.johnrengelman.shadow"

group 'me.deftware'
version '13.0'

sourceCompatibility = targetCompatibility = 1.8
compileJava {
    sourceCompatibility = targetCompatibility = 1.8
}

minecraft {
    version = '1.12.2'
    runDir = 'run'
    tweakClass = 'me.deftware.launch.Launcher'
    mappings = 'snapshot_20170906'
    useDepAts = true

    makeObfSourceJar = true
}

repositories {
    mavenCentral()

    maven {
        name = 'spongepowered-repo'
        url = 'http://repo.spongepowered.org/maven/'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }

}

configurations {
    deps
    compile {
        extendsFrom deps
    }
}


dependencies {
    compile('org.reflections:reflections:0.9.11') {
        exclude module: 'google-collections'
        exclude module: 'guava'
    }
    compile('org.spongepowered:mixin:0.6.10-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, "mixins.client.refmap.json"
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin'))
        include(dependency('org.reflections:reflections'))
    }

    manifest {
        attributes 'Implementation-Title':   'EMC'
        attributes 'Implementation-Version': version
    }

    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    classifier = 'full'
}

task createPom {
    doLast {
        pom {
            project {
                groupId "me.deftware"
                artifactId "EMC"
                version version
            }
        }.writeTo("build/libs/EMC-13.0.pom")
    }
}

reobf {
    shadowJar {
        mappingType = 'NOTCH'
        classpath = sourceSets.main.compileClasspath
    }
}

build.dependsOn(shadowJar)
build.dependsOn(createPom)
compileTestJava.dependsOn(shadowJar)
